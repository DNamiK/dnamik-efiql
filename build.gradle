plugins {
	id "jacoco"
	id "io.freefair.lombok" version "$freeFairLombokVersion"
	id "org.springframework.boot" version "$springBootVersion" apply false
}

subprojects {
	apply plugin: "java"
	apply plugin: "jacoco"
    apply plugin: "signing"
	apply plugin: "java-library"
	apply plugin: "maven-publish"
	apply plugin: "io.freefair.lombok"
	apply plugin: "io.spring.dependency-management"

	sourceCompatibility = 11
	targetCompatibility = 11

	repositories {
		mavenLocal()
		mavenCentral()
	}

	tasks.withType(JavaCompile) {
		options.encoding = "UTF-8"
		options.compilerArgs << "-parameters"
	}

	task javadocJar(type: Jar) {
		classifier = "javadoc"
		from javadoc
	}

	task sourcesJar(type: Jar) {
		classifier = "sources"
		from sourceSets.main.allSource
	}
	
	task packageJavadoc(type: Jar, dependsOn: "javadoc") {
		from javadoc.destinationDir
		classifier = "javadoc"
	}

	task packageSources(type: Jar, dependsOn: "classes") {
		from sourceSets.main.allSource
		classifier = "sources"
	}

	javadoc {
		source = sourceSets.main.allJava
	}

	artifacts {
		archives javadocJar, sourcesJar
	}

	test {
		useJUnitPlatform()
	}

	jacocoTestReport {
		dependsOn test
	}

	if (!version.toString().endsWith("-SNAPSHOT")) {
		ext["signing.keyId"] = System.getenv("SIGNING_KEY_ID")
		ext["signing.password"] = System.getenv("SIGNING_PASSWORD")
		ext["signing.secretKeyRingFile"] = System.getenv("SIGNING_SECRET_KEY_RING_FILE")

		signing {
			sign publishing.publications
		}
	}

	dependencyManagement {
		imports {
			mavenBom(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
		}
	}

	publishing {
		publications {
			mavenJava(MavenPublication) {
				from components.java
				artifact javadocJar
				artifact sourcesJar
				versionMapping {
					usage("java-api") {
						fromResolutionOf("runtimeClasspath")
					}
					usage("java-runtime") {
						fromResolutionResult()
					}
				}
				pom {
					name = project.getName()
					description = "DNamiK EFIQL - Extended Feed Item Query Language"
					url = "https://github.com/dnamik/dnamik-efiql"
					licenses {
						license {
							name = "GNU LGPLv3"
							url = "https://www.gnu.org/licenses/lgpl-3.0.txt"
							comments = "Incorporates the terms and conditions of https://www.gnu.org/licenses/gpl-3.0.txt"
							distribution = "repo"
						}
					}
					developers {
						developer {
							organization = "DNamiK"
							name = "Kristijan Georgiev"
							email = "kristijan.georgiev@gmail.com"
							organizationUrl = "https://github.com/dnamik"
						}
					}
					scm {
						url = "https://github.com/dnamik/dnamik-efiql"
						connection = "scm:git:git://github.com/dnamik/dnamik-efiql.git"
						developerConnection = "scm:git:git@github.com:dnamik/dnamik-efiql.git"
					}
				}
			}
		}

		repositories {
			maven {
				if (project.version.endsWith("-SNAPSHOT")) {
					url "https://s01.oss.sonatype.org/content/repositories/snapshots/"
				} else {
					url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
				}
				credentials {
					username System.getenv("NEXUS_RM_USERNAME")
					password System.getenv("NEXUS_RM_PASSWORD")
				}
			}
		}
	}

}
